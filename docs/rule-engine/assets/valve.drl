package rules

import pt.sharespot.iot.core.sensor.model.data.SensorDataDetailsDTO;
import pt.sharespot.iot.core.sensor.model.SensorDataDTO;
import pt.sharespot.iot.core.sensor.model.device.records.DeviceRecordEntryDTO;
import pt.sharespot.iot.core.sensor.model.properties.PropertyName;
import pt.sharespot.iot.core.alert.model.AlertBuilder;
import pt.sharespot.iot.core.alert.model.AlertLevel;
import pt.sharespot.iot.core.alert.model.CorrelationDataBuilder;
import java.util.List;
import java.util.UUID;

global pt.sharespot.iot.core.alert.model.AlertDispatcherService dispatcher;

dialect "mvel"

declare ValveDevice
    @role( event )
    deviceId : UUID
end

declare ValveDeviceData
    @role( event )
    deviceId : UUID
    dataId : UUID
    on : Boolean    
end

rule "Valve Alarm - Guard Scenario"
    when
        SensorDataDTO(
            !getSensorData().hasProperty(PropertyName.TRIGGER)
        )
    then
end

//to ensure that only one alarm per valve is sent
rule "Create new valve device that belongs to Project #003"
    when
        data : SensorDataDTO(
            getSensorData().hasProperty(PropertyName.TRIGGER)
        )
        exists DeviceRecordEntryDTO(label == "Project" && content == "#003") from data.device.records
        not(ValveDevice(deviceId == data.device.id))
    then
        ValveDevice valve = new ValveDevice();
        valve.setDeviceId(data.device.id);
        insert(valve)
        dispatcher.publish(AlertBuilder.create()
                          .setCategory("new device")
                          .setSubCategory("valve")
                          .setLevel(AlertLevel.INFORMATION)
                          .setDescription("DeviceId: " + data.device.id)
                          .build());
end

//if the data is from a valve device and is not duplicated add it
rule "Collect valve device data that belongs to Project #003"
    when
        data : SensorDataDTO(
            getSensorData().hasProperty(PropertyName.TRIGGER)
        )
        exists DeviceRecordEntryDTO(label == "Project" && content == "#003") from data.device.records
        not(ValveDeviceData(dataId == data.dataId))
    then
        ValveDeviceData reading = new ValveDeviceData();
        reading.setDeviceId(data.device.id);
        reading.setDataId(data.dataId);
        reading.setOn(data.getSensorData().trigger.value);
        insert(reading)
        dispatcher.publish(AlertBuilder.create()
                          .setCategory("data")
                          .setSubCategory("valve")
                          .setLevel(AlertLevel.INFORMATION)
                          .setDescription("DataId: " + data.dataId + " - On: " + data.getSensorData().trigger.value)
                          .setContext(CorrelationDataBuilder.create()
                                .setDataIds(data.dataId)
                                .setDeviceIds(data.device.id)
                                .build())
                          .build());
end

rule "Dispatch Valve Alarm - Senario 1 - Project #003"
    when
        $v : ValveDevice()
        $data: ValveDeviceData(deviceId == $v.deviceId, on == true)
        not(
            ValveDeviceData(
                this != $data, 
                on == false,
                this after[0s,10m] $data
            )
        )
    then
        dispatcher.publish(AlertBuilder.create()
                          .setCategory("smartIrrigation")
                          .setSubCategory("valveOpenForLengthyPeriod")
                          .setLevel(AlertLevel.ADVISORY)
                          .setDescription("Project #003 - Valve has been open for more than 10 minutes, needs to be closed")
                          .setContext(CorrelationDataBuilder.create()
                                .setDeviceIds($v.deviceId)
                                .build())
                          .build());
end
